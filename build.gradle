plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
}

version = '1.0'
group = 'extravideoset'
archivesBaseName = 'extra_video_settings'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.20.1'

    copyIdeResources = true

    runs {
        client {
        }

        server {
        }

        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            // Mixin refmap remapping: SRG -> official (dev環境でEmbeddiumのMixinを動作させる)
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${project.buildDir}/createSrgToMcp/output.srg"

            mods {
                extra_video_settings {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name = "Curse Maven"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.3.0'
    // Embeddium 0.3.31 for 1.20.1
    compileOnly fg.deobf("curse.maven:embeddium-908741:5681725")
    runtimeOnly fg.deobf("curse.maven:embeddium-908741:5681725")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
